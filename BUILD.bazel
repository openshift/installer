load("//bazel-rules:gen_test.bzl", "gen_test")
load("//:version.bzl", "OPENSHIFT_VERSION")

package(
    default_visibility = ["//visibility:public"],
)

config_setting(
    name = "darwin",
    values = {"cpu": "darwin"},
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"},  # don't ask...
    visibility = ["//visibility:public"],
)

genrule(
    name = "terraform_runtime_fmt",
    srcs = select({
        "//:linux": ["@terraform_runtime_linux//:terraform"],
        "//:darwin": ["@terraform_runtime_darwin//:terraform"],
    }),
    outs = ["bin/terraform"],
    cmd = "cp $(<) $(@)",
    executable = True,
    output_to_bindir = 1,
)

load("@io_bazel_rules_go//go:def.bzl", "go_prefix")

go_prefix("github.com/openshift/installer")

load("@bazel_gazelle//:def.bzl", "gazelle")

gazelle(
    name = "gazelle",
    command = "fix",
    external = "vendored",
    prefix = "github.com/openshift/installer",
)

alias(
    name = "smoke_tests",
    actual = "//tests/smoke:smoke",
)

alias(
    name = "cli",
    actual = "//installer/cmd/openshift-install",
)

template_files = glob([
    "modules/**/*",
    "steps/**/*",
    "config.tf",
])

exports_files(template_files)

filegroup(
    name = "template_resources",
    srcs = template_files,
)

load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")

alias(
    name = "tarball",
    actual = ":openshift-installer-%s" % OPENSHIFT_VERSION,
)

pkg_tar(
    name = "openshift-installer-%s" % OPENSHIFT_VERSION,
    mode = "0666",
    srcs = [
        "//:template_resources",
        "//examples:tectonic_cli_examples",
    ],
    extension = "tar.gz",
    package_dir = "openshift-installer-%s" % OPENSHIFT_VERSION,
    strip_prefix = ".",
    deps = [
        ":cli_bin",
        ":tf_bin",
    ],
)

pkg_tar(
    name = "cli_bin",
    mode = "0777",
    srcs = ["//installer/cmd/openshift-install"],
    package_dir = "installer",
)

filegroup(
    name = "terraform_runtime",
    srcs = select({
        "//:linux": ["@terraform_runtime_linux//:terraform"],
        "//:darwin": ["@terraform_runtime_darwin//:terraform"],
    }),
)

pkg_tar(
    name = "tf_bin",
    mode = "0777",
    srcs = [":terraform_runtime"],
    package_dir = "installer",
)

filegroup(
    name = "terraform_files",
    srcs = glob(
        ["modules/**/*.tf"],
        exclude_directories = 1,
    ) + glob(
        ["steps/**/*.tf"],
        exclude_directories = 1,
    ) + ["config.tf"],
)

gen_test(
    name = "terraform_fmt",
    size = "small",
    command = "./bin/terraform fmt -list -check -write=false",
    deps = [
        ":terraform_files",
        ":terraform_runtime_fmt",
    ],
)
